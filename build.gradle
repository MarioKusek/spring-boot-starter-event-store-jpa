plugins {
	id 'java-library'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'maven-publish'
	id "com.github.ben-manes.versions" version '0.51.0'
	id 'org.jreleaser' version '1.12.0'
}

group = 'hr.fer.tel'
version = '0.2.0'

ext {
    set('testcontainers.version', "1.19.8")
}

java {
	sourceCompatibility = '21'
    withJavadocJar()
    withSourcesJar()
}

repositories {
	mavenCentral()
}

dependencies {
	annotationProcessor "org.springframework.boot:spring-boot-autoconfigure-processor"
	
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'io.hypersistence:hypersistence-utils-hibernate-63:3.7.6'
	runtimeOnly 'org.postgresql:postgresql'
	
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
}

tasks.named('test') {
	useJUnitPlatform()
}

bootJar {
    enabled = false
}

jar {
	archiveClassifier = ''
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "${group}"
            artifactId = "${project.name}"
            version = "${version}"
            from components.java
            
            pom {
                description = 'This is event store Spring Boot starter the is using JPA to store events.'
                name = 'Event store JPA Spring Boot starter'
                url = 'https://github.com/MarioKusek/spring-boot-starter-event-store-jpa'
			
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    url = 'https://github.com/MarioKusek/spring-boot-starter-event-store-jpa'
                    connection = 'scm:git:https://github.com/MarioKusek/spring-boot-starter-event-store-jpa.git'
                    developerConnection = 'scm:git:ssh://git@github.com:MarioKusek/spring-boot-starter-event-store-jpa.git'
                }

                developers {
                    developer {
                        name = 'Mario Ku≈°ek'
                        email = "mario.kusek@fer.hr"
                    }
                }
            }
        }
    }
    
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}


jreleaser {
  signing {
    active = 'ALWAYS'
    //armored = true
    mode = 'FILE'
  }
  deploy {
    maven {
/*
	  nexus2 {
                'maven-central' {
                    active = 'ALWAYS'
                    url = 'https://s01.oss.sonatype.org/service/local'
                    snapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    closeRepository = false
                    releaseRepository = false
                    stagingRepository('build/staging-deploy')
                }
      }
*/
      mavenCentral {
        sonatype {
          active = 'ALWAYS'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository('build/staging-deploy')
        }
      }
    }
  }
}
